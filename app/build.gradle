apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.triplet.play'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 27
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "com.johnguant.redditthing"
        minSdkVersion 15
        targetSdkVersion 27
        if(System.getenv("CIRCLE_BUILD_NUM")){
            versionCode System.getenv("CIRCLE_BUILD_NUM") as Integer
            versionName "alpha-" + System.getenv("CIRCLE_BUILD_NUM")
        } else {
            versionCode 1
            versionName "0.0.1"
        }
        resConfigs "en"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        warning 'InvalidPackage'
    }

    adbOptions {
        timeOutInMs 300000 // 5 seconds in ms.
    }
}

play {
    jsonFile = file('keys.json')
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:27.0.0'
    compile 'com.android.support:design:27.0.0'
    compile 'com.android.support:support-v4:27.0.0'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.android.support:customtabs:27.0.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile("org.jetbrains.anko:anko:$anko_version") {
        exclude group: 'com.google.android', module: 'android'
    }
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.bignerdranch.android:expandablerecyclerview:3.0.0-RC1'
    compile 'co.zsmb:materialdrawer-kt:1.2.1'
}
repositories {
    mavenCentral()
}
kotlin {
    experimental {
        coroutines "enable"
    }
}
